apply plugin: 'java-gradle-plugin'
apply plugin: 'org.jetbrains.kotlin.jvm'
apply plugin: "com.google.devtools.ksp"
apply plugin: 'org.jetbrains.dokka'

ext {
    artifactName = publish_gradle_plugin_artifact
    pomName = 'Poko Gradle Plugin'
    basePackage = 'dev.drewhamilton.poko.gradle'
}
apply from: '../publish.gradle'
apply from: '../info.gradle'

compileKotlin {
    kotlinOptions {
        jvmTarget = System.getenv().getOrDefault('ci_java_version', JavaVersion.VERSION_1_8.toString())
        freeCompilerArgs += ['-progressive']
    }
}

gradlePlugin {
    plugins {
        pokoPlugin {
            id = 'dev.drewhamilton.poko'
            implementationClass = 'dev.drewhamilton.poko.gradle.PokoGradlePlugin'
        }
    }
}

dependencies {
    implementation(libs.kotlin.gradlePluginApi)
    compileOnly(libs.kotlin.gradlePlugin)

    implementation(libs.autoService.annotations)
    ksp(libs.autoService.ksp)

    testImplementation libs.junit
    testImplementation libs.truth
}
