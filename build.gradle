import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

group publish_group
version publish_version

buildscript {
    repositories {
        mavenCentral()

        // KSP:
        google()
    }

    dependencies {
        classpath libs.kotlin.gradlePlugin
        classpath libs.dokka.gradlePlugin
        classpath libs.kotlinx.binaryCompatibilityValidator

        classpath libs.ksp.gradlePlugin
    }
}

apply plugin: 'binary-compatibility-validator'

String resolvedJvmTarget = System.getenv().getOrDefault('ci_java_version', JavaVersion.VERSION_1_8.toString())
allprojects {
    repositories {
        mavenCentral()

        // KSP:
        google()
    }

    pluginManager.withPlugin("org.jetbrains.kotlin.jvm") {
        project.tasks.withType(KotlinCompile.class).configureEach {
            kotlinOptions {
                jvmTarget = resolvedJvmTarget
                freeCompilerArgs += ['-progressive']
            }
        }
    }
}
