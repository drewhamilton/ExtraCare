import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

buildscript {
    // Shared gradle.properties
    Properties properties = new Properties()
    file("../gradle.properties").withInputStream { properties.load(it) }
    properties.each { key, val ->
        ext.set(key, val)
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "$publish_group:$publish_gradle_plugin_artifact"
    }
}

apply plugin: 'org.jetbrains.kotlin.jvm'
apply plugin: 'dev.drewhamilton.poko'

poko {
    pokoAnnotation.set 'dev.drewhamilton.poko.sample.Poko'
    enabled.set true
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    testImplementation 'junit:junit:4.13.1'
    testImplementation 'com.google.truth:truth:1.1'
}

repositories {
    mavenCentral()
}

boolean useIr = Boolean.parseBoolean(project.property("use_ir").toString())
logger.lifecycle "$project: useIR = $useIr"
tasks.withType(KotlinCompile.class).configureEach {
    kotlinOptions {
        useIR = useIr
    }
}
