import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

group publish_group
version publish_version

buildscript {
    repositories {
        mavenCentral()

        // TODO: Remove Bintray dependencies when possible
        maven { url 'https://dl.bintray.com/kotlin/dokka' }
        maven { url 'https://dl.bintray.com/jetbrains/markdown' }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokka_version"
        classpath "org.jetbrains.kotlinx:binary-compatibility-validator:$binary_compatibility_validator_version"
    }
}

apply plugin: 'binary-compatibility-validator'

String resolvedJvmTarget = System.getenv().getOrDefault('ci_java_version', JavaVersion.VERSION_1_8.toString())
allprojects {
    repositories {
        // TODO WORKAROUND: https://github.com/JetBrains/markdown/issues/70
        maven { url 'https://dl.bintray.com/jetbrains/markdown' }

        mavenCentral()

        // TODO: Remove Bintray dependencies when possible
        maven { url 'https://dl.bintray.com/korlibs/korlibs' }
        maven { url 'https://dl.bintray.com/kotlin/dokka' }
        maven { url 'https://dl.bintray.com/kotlin/kotlinx' }
    }

    pluginManager.withPlugin("org.jetbrains.kotlin.jvm") {
        project.tasks.withType(KotlinCompile.class).configureEach {
            kotlinOptions {
                jvmTarget = resolvedJvmTarget
                freeCompilerArgs += ['-progressive']
            }
        }
    }
}
